#include 'tlpp-core.th'
#include 'tlpp-rest.th'

Namespace TotvsDevelopers.Demo.Controllers

Using Namespace TotvsDevelopers.Demo.Services

Class WarehouseController From BaseController
    Private Data oService

    Public Method New() Constructor

    @Get("/api/v1/warehouses")
    Public Method Get()

EndClass

Method New() Class WarehouseController
    _Super:New()
Return

Method Get() Class WarehouseController
    Local jQuery := oRest:GetQueryRequest()
    Local jResult := Nil

    Self:oService := WarehouseService():New()
    jResult := Self:oService:GetPage(  if(!Empty(jQuery['page']),Val(jQuery['page']), nil),  if(!Empty(jQuery['pageSize']),Val(jQuery['pageSize']), nil) )

    If ( jResult['success'] )
        oRest:SetResponse( jResult['result'] )
    Else
        oRest:SetFault( jResult['result']:Tojson() )
        oRest:SetStatusCode( jResult['result']['code'] )
    EndIf
Return

